# This script tests counterarguments and undercut.

lk.

# First we declare our running example:
#
declare EfficientMetro, StrikeMetro, UseMetro, DealMetro, LowballOffer : bool.
declare r1: (EfficientMetro -> UseMetro).
declare r2: (StrikeMetro -> ~EfficientMetro).
#
# Then an argument to use the metro.
#
start argument arg1 UseMetro
cut (EfficientMetro -> UseMetro) th.
axiom r1.
elim.
next.
axiom.
end argument
#
# Next, a counterargument in negation-expanded form
#
start counterargument arg2 EfficientMetro

cut (~EfficientMetro) x.
cut (StrikeMetro -> ~EfficientMetro) th.
axiom r2.
elim.
next.
axiom.
elim.
axiom.
end argument
#
# Now we test if arg2 undercuts arg1 on EfficientMetro as intended: 
#
undercut u1 arg2 arg1
#
# Next, we reduce the resulting argument to confirm that arg1 is defeated and color it.
reduce u1
color u1
#
#We add an attacker to the counterargument to test that the original conclusion is restored as intended.
#
#
declare r3: (DealMetro -> ~StrikeMetro).
#
start counterargument arg3 StrikeMetro
cut (~StrikeMetro) x.
cut (DealMetro -> ~StrikeMetro) th.
axiom r3.
elim.
next.
axiom.
elim.
axiom.
end argument
#
# Introduce the attacker, normalize and color:
undercut u2 arg3 u1
reduce u2
color u2
#
# And one last undercut
#
declare r4: (LowballOffer -> ~DealMetro).
#
start counterargument arg4 DealMetro
cut (~DealMetro) x.
cut (LowballOffer -> ~DealMetro) th.
axiom r4.
elim.
next.
axiom.
elim.
axiom.
end argument
#
# Introduce the attacker, normalize and color:
undercut u3 arg4 u2
reduce u3
color u3
tree u3

